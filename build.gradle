buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'checkstyle'
    id 'jacoco'
    id 'pmd'
    id 'maven-publish'
    id 'signing'
    id 'org.springframework.boot' version '2.4.13'
    id "org.flywaydb.flyway" version "8.0.5"
    id "org.sonarqube" version "3.3"
    id "com.moowork.node" version "1.0.0"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = 8
}

ext {
    ossrhUsername = System.getenv("OSSRH_USERNAME")
    ossrhPassword = System.getenv("OSSRH_PASSWORD")
    set('springBootVersion', '2.4.13')
    set("signing.secretKeyRingFile", ".signing/secring.gpg")
    set("signing.keyId", System.getenv("SIGNING_KEYID"))
    set("signing.password", System.getenv("SIGNING_PASSWORD"))
}

group = serviceGroup
version = serviceVersion
archivesBaseName = rootProject.name
project.ext.buildTime = java.time.Instant.now().toString() // for versioning

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jasperreports.sourceforge.net/maven2/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://jaspersoft.jfrog.io/artifactory/third-party-ce-artifacts"}
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web:2.4.13"
    implementation "org.springframework.boot:spring-boot-starter-data-rest:2.4.13"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:2.4.13"
    implementation "org.springframework.boot:spring-boot-starter-security:2.4.13"
    implementation "org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:2.6.8"
    implementation 'org.springframework:spring-context-support:5.3.13'
    implementation 'org.hibernate:hibernate-java8:5.4.9.Final'
    implementation 'org.flywaydb:flyway-core:8.0.5'
    implementation 'org.openlmis:openlmis-service-util:3.0.0'

    implementation "org.projectlombok:lombok:1.18.22"
    implementation "org.postgresql:postgresql:42.6.2"
    implementation "com.google.guava:guava:18.0"
    implementation 'org.webjars.npm:api-console:3.0.17'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.apache.commons:commons-csv:1.4'
    implementation 'org.apache.commons:commons-collections4:4.1'
    implementation 'commons-io:commons-io:2.5'
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    implementation "net.sf.jasperreports:jasperreports:6.5.1"
    implementation "org.slf4j:slf4j-ext:1.7.29"
    implementation "com.github.tomakehurst:wiremock:1.58"

    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    implementation group: 'javax.interceptor', name: 'javax.interceptor-api', version: '1.2'

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.mockito:mockito-core:3.6.28"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation "org.powermock:powermock-api-mockito2:2.0.9"
    testImplementation "org.powermock:powermock-module-junit4:2.0.9"
    testImplementation "org.springframework.boot:spring-boot-starter-test:2.4.13"
    testImplementation 'org.springframework.security:spring-security-test:5.3.9.RELEASE'
    testImplementation "org.springframework:spring-test:5.3.13"
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

    testImplementation "com.jayway.restassured:rest-assured:2.7.0"
    testImplementation ("guru.nidi.raml:raml-tester:0.8.15") {
        exclude group: "org.apache.logging.log4j", module: "log4j-slf4j-impl"
    }
    testImplementation ("org.raml:raml-parser:0.8.37") {
        exclude group: "org.apache.logging.log4j", module: "log4j-slf4j-impl"
    }
    testImplementation "nl.jqno.equalsverifier:equalsverifier:3.10.1"
    testImplementation "be.joengenduvel.java.verifiers:to-string:1.0.2"


    implementation(fileTree(dir: '/extensions/', include: ['*.jar']))
}

idea {
    project {
        vcs = 'Git'
        ipr.withXml { xmlFile ->
            // enable 'Annotation Processors', source: https://gist.github.com/stephanos/8645809
            xmlFile.asNode().component
                    .find { it.@name == 'CompilerConfiguration' }['annotationProcessing'][0]
                    .replaceNode {
                annotationProcessing {
                    profile(default: true, name: 'Default', useClasspath: 'true', enabled: true)
                }
            }
        }
    }
    module {
        sourceDirs -= file('src/integration-test/java')
        testSourceDirs += file('src/integration-test/java')
    }
}

flyway {
    url = "$System.env.DATABASE_URL"
    user = "$System.env.POSTGRES_USER"
    password = "$System.env.POSTGRES_PASSWORD"
    schemas = ['stockmanagement']
    sqlMigrationPrefix = ''
    placeholderPrefix = '#['
    placeholderSuffix = ']'
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
    }
    mustRunAfter test
    environment 'BASE_URL', "http://localhost"
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
    reports.junitXml.destination = file("${testResultsDir}/${name}")
    environment 'BASE_URL', "http://localhost"
}

// Usage: gradle generateMigration [-PmigrationName=name_of_migration]
//          Defaults to 'migration' as migration name
// Example: gradle generateMigration -PmigrationName=add_column_to_users
//          Will create a file in migration folder with name yyyyMMddHHmmssSSS_add_column_to_users.sql.
task generateMigration {
    description 'Creates an empty new file within the src/main/resources/db/migration directory into which developers can add new SQL migration code.'
    doLast {
        def fileName = project.hasProperty('migrationName') ? migrationName : 'migration'
        def timestamp = new Date().format('yyyyMMddHHmmssSSS', TimeZone.getTimeZone('GMT'))
        def fullFileName = "${timestamp}__${fileName}.sql"
        def migrationFile = new File(sourceSets.main.resources.srcDirs.first(), 'db/migration/' + fullFileName)
        migrationFile << "-- WHEN COMMITTING OR REVIEWING THIS FILE: Make sure that the timestamp in the file name (that serves as a version) is the latest timestamp, and that no new migration have been added in the meanwhile.\n"
        migrationFile << "-- Adding migrations out of order may cause this migration to never execute or behave in an unexpected way.\n"
        migrationFile << "-- Migrations should NOT BE EDITED. Add a new migration to apply changes."
        migrationFile.createNewFile()
    }
}

task checkApiIsRaml(type: Exec) {
    executable "raml-cop"
    args "src/main/resources/api-definition.yaml"
}

configure(checkApiIsRaml) {
    group = JavaBasePlugin.VERIFICATION_GROUP
    description = 'Verify that the api-specification is valid RAML'
}

jacocoTestReport {
    dependsOn integrationTest, test
    group = "reporting"
    description = "Generate Jacoco coverage reports after running tests."

    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }

    executionData file("$buildDir/jacoco/test.exec"), file("$buildDir/jacoco/integrationTest.exec")
    additionalSourceDirs.setFrom(files(sourceSets.main.allJava.srcDirs))
}

checkstyle {
    toolVersion = "8.12"
}

//NOTE: This plugin requires that this task be named 'sonarqube'. In fact, it is performing SonarCloud analysis.
sonarqube {
    properties {
        property "sonar.projectKey", "OpenLMIS_openlmis-stockmanagement"
        property "sonar.organization", "openlmis"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.source", "17"
        property "sonar.coverage.jacoco.xmlReportPaths", "./report.xml"
    }
}

pmd {
    toolVersion = '6.55.0'
    consoleOutput = true
    ignoreFailures = true
    ruleSetFiles = files("config/pmd/ruleset.xml")
    reportsDir = file("build/reports/pmd")
}

tasks.withType(Pmd) {
    reports {
        xml.enabled true
        html.enabled true
    }
}

test {
    testLogging {
        events 'started', 'passed'
        exceptionFormat = 'full'
    }
    dependsOn checkstyleMain
    dependsOn checkstyleTest
    dependsOn pmdMain
    dependsOn pmdTest
}

//enable the lines below if you need to debug spring running in docker
//bootRun {
//    jvmArgs = ["-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005"]
//}

signing {
    sign configurations.archives
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task execJar(type: Jar) {
    classifier = 'exec'
    from sourceSets.main.output
}

artifacts {
    archives javadocJar, sourcesJar, execJar
}

signArchives.onlyIf {project.findProperty("signing.keyId") && project.findProperty("signing.password")}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = project.name
                description = project.description
                url = 'https://openlmis.org'

                licenses {
                    license {
                        name = 'GNU Affero General Public License, Version 3'
                        url = 'https://www.gnu.org/licenses/agpl-3.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'techcommittee'
                        name = 'tech committee'
                        email = 'info@openlmis.org'
                        organizationUrl = 'https://villagereach.org'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/OpenLMIS/openlmis-stockmanagement.git'
                    developerConnection = 'scm:git:ssh://github.com/OpenLMIS/openlmis-stockmanagement.git'
                    url = 'http://github.com/OpenLMIS/openlmis-stockmanagement/'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = version.endsWith("SNAPSHOT") ?
                    uri("https://oss.sonatype.org/content/repositories/snapshots/") :
                    uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(PublishToMavenRepository).configureEach {
    onlyIf {
        project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")
    }
}

apply from: "documentation.gradle"
integrationTest {
    dependsOn ramlToHtml
}

processResources {
    // we want the generated HTML spec file included in the output jar
    finalizedBy ramlToHtml

    // update version information in build
    filesMatching('**/version.properties') {
        expand(project.properties)
    }
}

check {
    dependsOn checkApiIsRaml
    dependsOn integrationTest
    finalizedBy publish
}

apply from: "registration.gradle"

build {
    dependsOn jacocoTestReport
    dependsOn check
    dependsOn ramlToHtml
    dependsOn copyRamlHtmlToBuild
    dependsOn copyConsulRegistrationToBuild
}
