import org.apache.tools.ant.filters.ReplaceTokens

def ramlToCopy = copySpec {
    from('src/main/resources') {
        include 'api-definition.yaml'
        into 'static/stockmanagement/docs'
        rename { 'api-definition.raml' }
        filter(ReplaceTokens, tokens: [
                baseUrl: "http://localhost",
                version: project.version
        ])
        filter { line ->
            line.replaceAll("\"schemas(.*).json\"", "\"#/schemas\$1\"")
        }
    }
    from('src/main/resources') {
        include 'api-definition.yaml'
        rename { 'api-definition-raml.yaml' }
        filter(ReplaceTokens, tokens: [
                baseUrl: "http://localhost",
                version: project.version
        ])
    }
    from('src/main/resources/schemas') {
        include '*.json'
        into 'schemas'
    }
    from('src/main/resources/schemas') {
        include '*.json'
        into 'static/stockmanagement/docs/schemas'
    }
}

tasks.register('copyRamlToBuild', Copy) {
    group = 'documentation'
    with ramlToCopy
    into 'build/resources/main'
}

tasks.register('ramlToHtml', Exec) {
    group = 'documentation'
    description = 'Convert RAML to HTML document'

    dependsOn 'npmInstall', 'copyRamlToBuild'

    commandLine 'npm', 'run', 'runApiHtmlConverter'
}

tasks.register('copyRamlHtmlToBuild', Copy) {
    group = 'documentation'
    dependsOn 'ramlToHtml'
    from 'build/resources/main'
    include 'api-definition.html'
    into 'build/resources/main'
}
